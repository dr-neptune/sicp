* Logic Programming 
:PROPERTIES:
:header-args: :session scheme :results verbatim raw
:ARCHIVE:
:END:

In a nondeterministic language, expressions can have more than one value, and, as a result, the computation is dealing with relations rather than with single-valued functions. Logic programming extends this idea by combining a relational vision of programming with a powerful kind of symbolic pattern matching called unification. 

When this approach works, we get a very powerful way to write programs. Part of that power comes from the fact that a single "what is" fact can be used to solve a number of different problems that would have different "how to" components. 

** Deductive Information Retrieval

Logic programming excels in providing interfaces to databases for information retrieval. 

** Simple Queries 

The query language allows users to retrieve information from the database by posing queries in response to the system's prompt. 

#+BEGIN_SRC scheme
; query
(job ?x (computer programmer))

; results
(job (Hacker Alyssa P) (computer programmer))
(job (Fect Cy D) (computer programmer))
#+END_SRC

The input query specifies that we are looking for entries in the database that match a certain pattern. 

We can describe the query language's processing of simple queries as follows: 

- The system finds all assignments to variables in the query pattern that satisfy the pattern -- that is, all sets of values for the variables such that if the pattern variables are instantiated with (replaced by) the values, the result is in the database.
- The system responds to the query by listing all instantiations of the query pattern with the variable assignments that satisfy it 

*** Exercise 4.55 

Give simple queries that retrieve the following information from the database: 

1. All people supervised by Ben Bitdiddle 

#+BEGIN_SRC scheme
(supervisor ?x (Bitdiddle Ben))
#+END_SRC

2. The names and jobs of all people in the accounting division 

#+BEGIN_SRC scheme
(job ?x (accounting . ?type))
#+END_SRC

3. The names and addresses of all people who live in slumerville 

#+BEGIN_SRC scheme
(address ?x (Slumerville . ?type))
#+END_SRC

** Compound Queries 

One thing that makes the query language a logic programming language is that the means of combination mirror the means of combination used in forming logical expressions: and, or, and not. 

We can chain them together like so: 

#+BEGIN_SRC scheme
(and (job ?person (computer programmer))
     (address ?person ?where))
#+END_SRC

In general, (and (q1)(q2)...(qn)) is satisfied by all sets of values for the pattern variables that simultaneously satisfy queries 1 through n. 
The same holds for or and not 

There is also a combining form called lisp-value. When lisp-value is the first element of a pattern, it specifies the next element is a lisp predicate to be applied to the rest of the instantiated elements as arguments. In general (lisp-value predicate arg1 ... argn) will be satisfied by assignments to the pattern variables for which the predicate applied to the instantiated arg1 ... argn is true. 

for example: 

#+BEGIN_SRC scheme
(and (salary ?person ?amount)
     (lisp-value > ?amount 30000))
#+END_SRC

*** Exercise 4.56 

Formulate compound queries that retrieve the following information: 

1. The names of all people who are supervised by Ben Bitdiddle and their addresses 

#+BEGIN_SRC scheme
(and (supervisor ?person (Bitdiddle Ben))
     (address ?person ?where))
#+END_SRC

2. All people whose salary is less than Bens, together with their salary and Bens salary 

#+BEGIN_SRC scheme
(and (salary (Bitdiddle Ben) ?amount)
     (salary ?person (lisp-value <= ?new-sal ?amount)))
#+END_SRC

3. All people who are supervised by someone who is not in the computer division, together with the supervisor's name and job 

#+BEGIN_SRC scheme
(and (supervisor ?person ?boss)
     (not (job ?boss (computer . ?type)))
     (job ?boss ?job))
#+END_SRC

** Rules 

In addition to primitive queries and compound queries, the query language provides means for abstracting queries. These are given by rules. 

The rule 

#+BEGIN_SRC scheme
(rule (lives-near ?person-1 ?person-2)
      (and (address ?person-1 (?town . ?rest-1))
           (address ?person-2 (?town . ?rest-2))
           (not (same ?person-1 ?person-2))))
#+END_SRC

specifies that two people live near each other if they live in the same town. 

The same relation is specified by a very simple rule 

#+BEGIN_SRC scheme
(rule (same ?x ?x))
#+END_SRC

The following rule declares that a person is a wheel in an organization if they supervise someone who is in turn a supervisor: 

#+BEGIN_SRC scheme
(rule (wheel ?person)
      (and (supervisor ?middle-manager ?person)
           (supervisor ?x ?middle-manager)))
#+END_SRC

The general form of a rule is 

#+BEGIN_SRC scheme
(rule <conclusion> <body>)
#+END_SRC

where conclusion is a pattern and body is any query. 

As in the case of compound procedures, rules can be used as parts of other rules, or even be defined recursively

For instance, this rule says that a staff person is outranked by a boss in the organization if the boss is the person's supervisor, or (recursively) if the person's supervisor is outranked by the boss. 

#+BEGIN_SRC scheme
(rule (outranked-by ?staff-person ?boss)
      (or (supervisor ?staff-person ?boss)
          (and (supervisor ?staff-person ?middle-manager)
               (outranked-by ?middle-manager ?boss))))
#+END_SRC

*** Exercise 4.57 

Define a rule that says that person 1 can replace person 2 if either person 1 does the same job as person 2 or someone who does person 1's job can also do person 2's job, and if person 1 and person 2 are not the same person 

#+BEGIN_SRC scheme
(rule (replace ?person1 ?person2)
      (and
       ; get p1 job 
       (job ?person1 ?job1)
       ; get p2 job
       (job ?person2 ?job2)
       ; does p1 do the same job as p2? 
       (or (can-do-job ?person1 ?job2)
           ; can someone else do both jobs?
           (and (can-do-job ?person3 ?job1)
                (can-do-job ?person3 ?job2)))
       ; is anyone the same?
       (and (not (same ?person1 ?person2))
            (not (same ?person2 ?person3)))))
#+END_SRC

Using your rule, give queries that find the following: 

1. All people who can replace Cy D. Fect 

#+BEGIN_SRC scheme
(replace ?person (D-Fect Cy))
#+END_SRC

2. All people who can replace someone who is being paid more than they are, together with the two salaries

#+BEGIN_SRC scheme
(and (replace ?person1 ?person2)
     (salary ?person1 ?amount1)
     (salary ?person2 ?amount2)
     (lisp-value < ?amount1 ?amount2))
#+END_SRC


*** Exercise 4.58 

Define a rule that says that a person is a "big shot" in a division if the person works in the division, but does not have a supervisor who works in the division. 

#+BEGIN_SRC scheme
(rule (big-shot ?person)
      (and (job ?person ?division . ?rest)
          (or (and (supervisor ?person ?boss)
                   (not (job ?boss ?division . ?rest)))
              (not (same ?person ?boss)))))
#+END_SRC

*** Exercise 4.59 

Ben adds all his weekly meetings to the Microshaft database by asserting the following: 

#+BEGIN_SRC scheme
(meeting accounting (Monday 9am))
(meeting administration (Monday 10am))
(meeting computer (Wednesday 3pm))
(meeting administration (Friday 1pm))
(meeting whole-company (Wednesday 4pm))
#+END_SRC

1. On Friday mornings, Ben wants to query the database for all the meetings that occur that day. What query should he use? 

#+BEGIN_SRC scheme
(meeting ?dept (Friday ?time))
#+END_SRC

2. Alyssa thinks it would be more useful to ask for her meetings by specifying her name. She designs a rule that says that a person's meetings include all whole-company meetings plus all meetings of that person's division. Fill in the body of her rule: 

#+BEGIN_SRC scheme
(rule (meeting-time ?person ?day-and-time)
      (or (meeting whole-company (?day-and-time))
          (and (job ?person (?dept . ?rest))
               (meeting ?dept (?day-and-time)))))
#+END_SRC

3. Alyssa wonders what meetings she has to attend on Wednesday. What query should she make to find out? 

#+BEGIN_SRC scheme
(meeting-time (P.Hacker Alyssa) (Wednesday . ?time))
#+END_SRC

*** Exercise 4.60 

Alyssa notices that when she gives a query that is symmetric, she gets both results back and there appears to be duplicates. Why does this happen? 

- This happens because the relation is symmetric, i.e. arb <-> bra

Is there a way to find a list of people who line near each other, in which each pair appears only once? 

You could sort each tuple alphabetically and remove duplicates. 

** Logic as Programs 

We can regard a rule as a kind of logical implication: If an assignment of values to pattern variables satisfies the body, then it satisfies the conclusion. Consequently, we can regard the query language as having the ability to perform logical deductions based upon the rules. 

Consider the append operation characterized by the following two rules: 

- For any list y, the empty list and y append to form y
- For any u, v, y, z, (cons u v) and y append to form (cons u z) if v and y append to form z

To express this in our query language, we define two rules for a relation: 

#+BEGIN_SRC scheme
; x and y append to form z
(append-to-form x y z)

; holds for any value of ?y
(rule (append-to-form () ?y ?y))

(rule (append-to-form (?u . ?v) ?y (?u . ?z))
      (append-to-form ?v ?y ?z))
#+END_SRC


Given these two rules, we can formulate queries that compute the append of two lists: 

#+BEGIN_SRC scheme
; input
(append-to-form (a b) (c d) ?z)

; result
(append-to-form (a b) (c d) (a b c d))
#+END_SRC

We can also ask for all pairs of lists that append to form (a b c d)

#+BEGIN_SRC scheme
(append-to-form ?x ?y (a b c d))

(() (a b c d))
((a) (b c d))
((a b) (c d))
((a b c) (d))
((a b c d) ())
#+END_SRC

*** Exercise 4.61 

The following rules implement a next-to relation that finds adjacent elements of a list 

#+BEGIN_SRC scheme
(rule (?x next-to ?y in (?x ?y . ?u)))
(rule (?x next-to ?y in (?v . ?z))
      (?x next-to ?y in ?z))
#+END_SRC

What will be the response for the following queries?

#+BEGIN_SRC scheme
; query 
(?x next-to ?y in (1 (2 3) 4))

; response
(1 (2 3))
((2 3) 4)
#+END_SRC

#+BEGIN_SRC scheme
; query
(?x next-to 1 in (2 1 3 1))

; response
(2)
(3)
#+END_SRC

*** Exercise 4.62 

Define rules to implement the last-pair operation, which returns a list containing the last element of a nonempty list.

#+BEGIN_SRC scheme
(rule (last-pair (?x) (?x)))
(rule (last-pair (?u . ?v) ?x)
      (last-pair ?v (?x)))
#+END_SRC

Check the rules on queries like 

#+BEGIN_SRC scheme
(last-pair (3) ?x)

; result
(last-pair (3) (3))

(last-pair (1 2 3) ?x)

; result
(last-pair (2 3) (3))

(last-pair (2 ?x) (3))

; result 
(last-pair (2 3) (3))

(last-pair ?x (3))

; result
(error "???")
#+END_SRC

*** Exercise 4.63 

Formulate rules such as 

if S is the son of F, and F is the son of G, then S is the grandson of G 

#+BEGIN_SRC scheme
(rule (grandson ?s ?g)
      (and (son ?s ?f)
           (son ?f ?g)))
#+END_SRC

if W is the wife of M, and S is the son of W, then S is the son of M 

#+BEGIN_SRC scheme
(rule (step-son ?m ?s)
      (and (wife ?w ?m)
           (son ?s ?w)))
#+END_SRC

** How the Query System Works 

This section gives an overview of the general structure of the system for the logical programming language, independent of low-level implementation details. 

The query evaluator must perform some kind of search in order to match queries against facts and rules in the database. One way to do this is with amb. Another possibility is to manage the search with the aid of streams, which this implementation will follow. Our query system is organized around two operations called pattern matching and unification. 

Pattern matching, together with the organization of information in terms of streams of frames, enables us to implement simple and complex queries. 

Unification is a generalization of pattern matching needed to implement rules. 

** Pattern Matching 

A pattern matcher is a program that tests whether some datum fits a specified pattern. The pattern matcher used by the query system takes as inputs a pattern, a datum, and a frame that specifies bindings for various pattern variables. It checks whether the datum matches the pattern in a way that is consistent with the bindings already in the frame. The pattern matcher is needed to process simple queries that don't involve rules. 

** Steams of Frames

Given a single frame, the matching process runs through the database entries one by one. For each entry, the matcher generates either a special symbol indicating that the match has failed or an extension to the frame. The results for all the database entries are collected into a stream, which is passed through a filter to weed out failures. The result is a stream of all the frames that extend the given frame via a match to some assertion in the database. 

In general, since pattern matching is an expensive operations, we wish to do this filtering in two passes: first a coarse filter, then a finer filter. Ideally we would do this at the creation time of the database, which is called indexing. 

To answer a simple query, we use the query with an input stream consisting of a single empty frame. The resulting output stream contains all extensions to the empty frame. The stream of frames is then used to generate a stream of copies of the original query pattern with the variables instantiated by the values in each frame, and this stream is then printed.

** Compound Queries 

The elegance of the stream-of-frames implementation is evident when we consider compound queries. We can think of these as series of filters performed by simple queries, i.e.,

#+BEGIN_SRC scheme
(and (can-do-job ?x (computer programmer trainee))
     (job ?person ?x))
#+END_SRC

is a simple query for can-do-job passed to the next query for job.

Similarly or is two streams that are then merged. Not is a stream that returns all the values that the pattern matcher would otherwise drop. 

** Unification

Rule conclusions are like assertions except that they contain variables, so we will need a generalization of pattern matching -- called unification -- in which both the pattern and the datum may contain variables. 

A unifier takes two patterns, each containing constants and variables, and determines whether it is possible to assign values to the variables that will make the two patterns equal. If so, it returns a frame containing these bindings. The unifier used in the query system, like the pattern matcher, take a frame as input and perform unifications that are consistent with this frame.

** Applying Rules 

Unification is the key to the component of the query system that makes inference from rules. 

In general, the query evaluator uses the following method to apply a rule when trying to establish a query pattern in a frame that specifies bindings for some of the pattern variables:

- Unify the query with the conclusion of the rule to form, if successful, an extension of the original frame
- Relative to the extended frame, evaluate the query formed by the body of the rule

Notice how similar this is for applying a procedure in the eval-apply evaluator for lisp: 

- Bind the procedure's parameters to its arguments to form a frame that extends the original procedure environment
- Relative to the extended environment, evaluate the expression formed by the body of the procedure

** Simple Queries 

Given the query pattern and a stream of frames, we produce, for each frame in the input stream, two streams: 

- a stream of extended frames obtained by matching the pattern against all assertions in the database (using the pattern matcher)
- a stream of extended frames obtains by applying all possible rules (using the unifier)

Since unification is a generalization of pattern matching, we could use the unifier for both steps, but this illustrates the usefulness of the pattern matcher in its own right. 

** Is Logic Programming Mathematical Logic? 

The operations and, or, not, and the application of query language rules is accomplished through a legitimate method of inference. This identification of the query language with mathematical logic is not really valid though because the query language provides a control structure that interprets the logic statements procedurally. 

The aim of logic programming is to provide the programmer with two techniques for decomposing a computational problem into two separate problems: "what" is to be computed, and "how" this should be computed. Control ("how" to compute) is effected by using the order of evaluation of the language. At the same time, we should be able to view the result of the computation ("what" to compute) as a simple consequence of the laws of logic. 

** Infinite Loops 

A consequence of the procedural interpretation of logic programs is that it is possible to construct hopelessly inefficient programs for solving certain problems. An extreme case of inefficiency occurs when the system falls into infinite loops making deductions. 

** Problems with not 

The not of programming languages reflects the closed world assumption that all relevant information has been included in the database. 


*** Exercise 4.64 

Louis Reasoner mistakenly deletes the outranked-by rule from the database. When he realizes, he rewrites it as such: 

#+BEGIN_SRC scheme
(rule (outranked-by ?staff-person ?boss)
      (or (supervisor ?staff-person ?boss)
          (and (outranked-by ?middle-manager ?boss)
               (supervisor ?staff-person ?middle-manager))))
#+END_SRC

When Aull Dewitt issues the query (outranked-by (Bitdiddle Ben) ?who) it goes into an infinite loop. Explain why. 

#+BEGIN_SRC scheme
(outranked-by (Bitdiddle Ben) ?who)

(rule (outranked-by (B B) ?boss)
      (or (supervisor (B B) ?boss)
          (and (outranked-by ?middle-manager ?boss)
               (supervisor (B B) ?middle-manager))))
#+END_SRC

When we call this rule after unification, it will call (outranked-by ?middle-manager ?boss) which is identical to our query (outranked-by ?staff-person ?boss) leading to an infinite loop.

*** Exercise 4.65 

Cy D. Fect gives a query to find all the wheels. To his surprise, it lists Oliver Warbucks four times. Why?

Here is the implementation of wheel: 

#+BEGIN_SRC scheme
(rule (wheel ?person ?middle-manager ?x)
      (and (supervisor ?middle-manager ?person)
           (supervisor ?x ?middle-manager)))
#+END_SRC

wheel will return any person for whom there exists a person with 2 degrees of separation. 

Therefore, we have 

Warbucks -> Ben -> Alyssa
Warbucks -> Ben -> Lem 
Warbucks -> Ben -> Cy 
Warbucks -> Scrooge -> Cratchet 
Bitdiddle -> Alyssa -> Louis 

*** Exercise 4.66 

Ben has been generalizing the system to provide statistics about the company. For example: 

#+BEGIN_SRC scheme
(sum ?amount
     (and (job ?x (computer programmer))
          (salary ?x ?amount)))
#+END_SRC

When he hears about Cy's problem from the last exercise, he realizes his simple accumulation scheme won't work. Why?

If he were to call sum on the wheels, he would get back 4 instances of Warbucks salary. Similarly, since there can be multiple streams for nonfiltered rules, the accumulation would overcount. 

*** Exercise 4.67

pass 

*** Exercise 4.68 

Define a rule to implement the reverse operation 

#+BEGIN_SRC scheme
(rule (reverse () ()))
(rule (reverse ?x . ?y)
      (?y . ?x))
#+END_SRC

*** Exercise 4.69 

#+BEGIN_SRC scheme
(rule (end-grandson (grandson)))
(rule (end-grandson (?x . ?rest))
      (end-grandson ?rest))

(rule ((great . ?rel) ?x ?y)
      (and (end-grandson ?rel)
           (son ?x ?z)
           (?rel ?z ?y)))
#+END_SRC

** Implementing the Query System 

** The Driver Loop and Instantiation 

The driver loop for the query system repeatedly reads input expressions. If the expression is a rule or assertion to be added to the database, then the information is added. Otherwise the expression is assumed to be a query. 

#+BEGIN_SRC scheme
(define input-prompt ";;; Query input:")
(define output-prompt ";;; Query results:")

(define (query-driver-loop)
  (prompt-for-input input-prompt)
  (let ((q (query-syntax-process (read))))
    (cond ((assertion-to-be-added? q)
           (add-rule-or-assertion!
            (add-assertion-body q))
           (newline)
           (display "Assertion added to database.")
           (query-driver-loop))
          (else
           (newline)
           (display output-prompt)
           (display-stream
            (stream-map
             (lambda (frame)
               ; transform input syntactically
               (instantiate q frame (lambda (v f) (contract-question-mark v))))
             ; evaluate query
             (qeval q (singleton-stream '()))))
           (query-driver-loop)))))

; to instantiate, copy exp replacing variables with values
(define (instantiate exp frame unbound-var-handler)
  (define (copy exp)
    (cond ((var? exp)
           (let ((binding (binding-in-frame exp frame)))
             (if binding
                 (copy (binding-value binding))
                 (unbound-var-handler exp frame))))
          ((pair? exp)
           (cons (copy (car exp))
                 (copy (cdr exp))))
          (else exp)))
  (copy exp))
#+END_SRC

** The Evaluator 

The qeval procedure takes an input a query and a stream of frames, and it returns a stream of extended frames. It identifies special forms by a data-directed dispatch using get and put. 

#+BEGIN_SRC scheme
(define (qeval query frame-stream)
  (let ((qproc (get (type query) 'qeval)))
    (if qproc
        (qproc (contents query) frame-stream)
        (simple-query query frame-stream))))
#+END_SRC

** Simple Queries 

The simple-query procedure takes as args a simple query (a pattern) together with a stream of frames, and it returns a stream formed by extending each frame by all database matches of the query. 

#+BEGIN_SRC scheme
(define (simple-query query-pattern frame-stream)
  (stream-flatmap
   (lambda (frame)
     (stream-append-delayed
      (find-assertions query-pattern frame)
      (delay (apply-rules query-pattern frame))))
   frame-stream))
#+END_SRC 

** Compound Queries 

and queries are handled by conjoin. conjoin takes as inputs the conjuncts and the frame stream and returns the stream of extended frames. 

#+BEGIN_SRC scheme
(define (conjoin conjuncts frame-stream)
  (if (empty-conjunction? conjuncts)
      frame-stream
      (conjoin (rest-conjuncts conjuncts)
               (qeval (first-conjunct conjuncts)
                      frame-stream))))

(put 'and 'qeval conjoin)
#+END_SRC

or queries are handled by disjoin 

#+BEGIN_SRC scheme
(define (disjoin disjuncts frame-stream)
  (if (empty-disjunction? disjuncts)
      the-empty-stream
      (interleave-delayed
       (qeval (first-disjunct disjunct)
              frame-stream)
       (delay (disjoin
               (rest-disjuncts disjuncts)
               frame-stream)))))

(put 'or 'qeval disjoin)
#+END_SRC

** Filters 

Not attempts to extend each frame in the input stream to satisfy the query being negated, and we include a given frame in the output stream if it cannot be extended. 

#+BEGIN_SRC scheme
(define (negate operands frame-stream)
  (stream-flatmap
   (lambda (frame)
     (if (stream-null? (qeval (negated-query operands)
                              (singleton-stream frame)))
         (singleton-stream frame)
         the-empty-stream))
   frame-stream))

(put 'not 'qeval negate)
#+END_SRC

lisp-value is similar to not. Each frame in the stream is used to instantiate the variables in the pattern, the indicated predicate is applied, and the frames for which the predicate returns false are filtered out of the input stream. 

#+BEGIN_SRC scheme
(define (lisp-value call frame-stream)
  (stream-flatmap
   (lambda (frame)
     (if (execute
          (instantiate call frame (lambda (v f) (error "Unknown pat var: LISP-VALUE" v))))
         (singleton-stream frame)
         the-empty-stream))))

(put 'lisp-value 'qeval lisp-value)

; execute applies the predicate to the arguments 
(define (execute exp)
  (apply (eval (predicate exp) user-initial-environment)
         (args exp)))

; provides a query that is always satisfied 
(define (always-true ignore frame-stream) frame-stream)
(put 'always-true 'qeval always-true)
#+END_SRC

** Finding Patterns by Pattern Matching 

find-assertions, called by simple-query, takes as input a pattern and a frame. It returns a stream of frames, each extending the given one by a database match of the given pattern. It uses fetch-assertions to get a stream of all the assertions in the database that should be checked for a match against the pattern and frame. 

#+BEGIN_SRC scheme
(define (find-assertions pattern frame)
  (stream-flatmap
   (lambda (datum) (check-an-assertion datum pattern frame))
   (fetch-assertions pattern frame)))

; takes a pattern, assertion, and a frame and returns either a singleton stream w extended frame or the empty stream if the match fails
(define (check-an-assertion assertion query-pat query-frame)
  (let ((match-result (pattern-match query-pat assertion query-frame)))
    (if (eq? match-result 'failed)
        the-empty-stream
        (singleton-stream match-result))))

; match pattern against the data, element by element
(define (pattern-match pat dat frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? pat dat) frame)
        ((var? pat) (extend-if-consistent pat dat frame))
        ((and (pair? pat)
              (pair? dat))
         (pattern-match (cdr pat)
                        (cdr dat)
                        (pattern-match (car pat)
                                       (car dat)
                                       frame)))
        (else 'failed)))

; extends a frame by adding a new binding, if consistent with the bindings already in the frame
(define (extend-if-consistent var dat frame)
  (let ((binding (binding-in-frame var frame)))
    (if binding
        (pattern-match (binding-value binding) dat frame)
        (extend var dat frame))))
#+END_SRC

** Rules and Unification 

apply-rules is the rule analog of find-assertions. 

#+BEGIN_SRC scheme
(define (apply-rules pattern frame)
  (stream-flatmap (lambda (rule)
                    (apply-a-rule rule pattern frame))
                  (fetch-rules pattern frame)))
#+END_SRC

#+BEGIN_SRC scheme
; rename variables to avoid namespace collision with rules
(define (apply-a-rule rule query-pattern query-frame)
  (let ((clean-rule (rename-variables-in rule)))
    (let ((unify-result (unify-match query-pattern
                                     (conclusion clean-rule)
                                     query-frame)))
      (if (eq? unify-result 'failed)
          the-empty-stream
          (qeval (rule-body clean-rule)
                 (singleton-stream unify-result))))))
#+END_SRC

#+BEGIN_SRC scheme
(define (rename-variables-in-rule)
  (let ((rule-application-id (new-rule-application-id)))
    (define (tree-walk exp)
      (cond ((var? exp)
             (make-new-variable exp rule-application-id))
            ((pair? exp)
             (cons (tree-walk (car exp))
                   (tree-walk (cdr exp))))
            (else exp)))
    (tree-walk rule)))
#+END_SRC

The unification algorithm is implemented as a procedure that takes as input two patterns and a frame and returns either the extended frame or the symbol failed. The unifier is like the pattern matcher except that it is symmetrical -- variables are allowed on both sides of the match. 

#+BEGIN_SRC scheme
(define (unify-match p1 p2 frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? p1 p2) frame)
        ((var? p1) (extend-if-possible p1 p2 frame))
        ((var? p2) (extend-if-possible p2 p1 frame))
        ((and (pair? p1)
              (pair? p2))
         (unify-match (cdr p1) (cdr p2)
                      (unify-match (car p1) (car p2) frame)))
        (else 'failed)))
#+END_SRC

In unification, as in one-sided pattern matching, we want to accept a proposed extension of the frame only if it is consistent with existing bindings. If the variable we wish to match is not bound, but the variable we are trying to match it with is itself a different variable, it is necessary to check to see if the value is bound, and if so, to match its value. If both parties to the match are unbound, we may bind either to the other. 

#+BEGIN_SRC scheme
(define (extend-if-possible var val frame)
  (let ((binding (binding-in-frame var frame)))
    (cond (binding (unify-match (binding-value binding) val frame))
          ((var? val)
           (let ((binding (binding-in-frame val frame)))
             (if binding
                 (unify-match var (binding-value binding) frame)
                 (extend var val frame))))
          ((depends-on? val var frame) 'failed)
          (else (extend var val frame)))))

; tests whether the expression proposed to be the value of a pattern variable depends on the variable
(define (depends-on? exp var frame)
  (define (tree-walk e)
    (cond ((var? e)
           (if (equal? var e)
               true
               (let ((b (binding-in-frame e frame)))
                 (if b
                     (tree-walk (binding-value b))
                     false))))
          ((pair? e)
           (or (tree-walk (car e))
               (tree-walk (cdr e))))
          (else false)))
  (tree-walk exp))
#+END_SRC

** Maintaining the Database 

One important problem in designing logic programming languages is that of arranging things so that as few irrelevant database entries as possible will be examined in checking a given pattern. 

#+BEGIN_SRC scheme
(define THE-ASSERTIONS the-empty-stream)

(define (fetch-assertions pattern frame)
  (if (use-index? pattern)
      (get-indexed-assertions pattern)
      (get-all-assertions)))

(define (get-all-assertions) THE-ASSERTIONS)

(define (get-indexed-assertions pattern)
  (get-stream (index-key-of pattern) 'assertion-stream))

(define (get-stream key1 key2)
  (let ((s (get key1 key2)))
    (if s
        s
        the-empty-stream)))

(define THE-RULES the-empty-stream)

(define (fetch-rules pattern frame)
  (if (use-index? pattern)
      (get-indexed-rules pattern)
      (get-all-rules)))

(define (get-all-rules) THE-RULES)

(define (get-indexed-rules pattern)
  (stream-append
   (get-stream (index-key-of-pattern) 'rule-stream)
   (get-stream '? 'rule-stream)))

(define (add-rule-or-association! assertion)
  (if (rule? assertion)
      (add-rule! assertion)
      (add-assertion! assertion)))

(define (add-assertion! assertion)
  (store-assertion-in-index assertion)
  (let ((old-assertions THE-ASSERTIONS))
    (set! the-assertions (cons-stream assertion old-assertions))
    'ok))

(define (add-rule! rule)
  (store-rule-in-index rule)
  (let ((old-rules THE-RULES))
    (set! THE-RULES (cons-stream rule old-rules))
    'ok))

; to store an assertion or rule, check to see if it can be indexed
(define (store-assertion-in-index assertion)
  (if (indexable? assertion)
      (let ((key (index-key-of assertion)))
        (let ((current-assertion-stream (get-stream key 'assertion-stream)))
          (put key 'assertion-stream
               (cons-stream assertion current-assertion-stream))))))

(define (store-rule-in-index rule)
  (let ((pattern (conclusion rule)))
    (if (indexable? pattern)
        (let ((key (index-key-of pattern)))
          (let ((current-rule-stream (get-stream key 'rule-stream)))
            (put key 'rule-stream (cons-stream rule current-rule-stream)))))))

; a pattern will be stored in the table if it starts with a variable or constant symbol
(define (indexable? pat)
  (or (constant-symbol? (car pat))
      (var? (car pat))))

(define (index-key-of pat)
  (let ((key (car pat)))
    (if (var? key) '? key)))

(define (use-index? pat)
  (constant-symbol? (car pat)))
#+END_SRC

** Stream Operations 

stream-append-delayed and interleave-delayed are just like stream-append and interleave except that they have a delayed argument. 

#+BEGIN_SRC scheme
(define (stream-append-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream (stream-car s1)
                   (stream-append-delayed (stream-cdr s1) delayed-s2))))

(define (interleave-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream (stream-car s1)
                   (interleave-delayed
                    (force delayed-s2)
                    (delay (stream-cdr s1))))))

(define (stream-flatmap proc s)
  (flatten-stream (stream-map proc s)))

(define (flatten-stream stream)
  (if (stream-null? stream)
      the-empty-stream
      (interleave-delayed
       (stream-car stream)
       (delay (flatten-stream (stream-cdr stream))))))

(define (singleton-stream x)
  (cons-stream x the-empty-stream))
#+END_SRC

** Query Syntax Procedures

type and contents, used by qeval, specify that a special form is identified by the symbol in its car. 

#+BEGIN_SRC scheme
(define (type exp)
  (if (pair? exp)
      (car exp)
      (error "Unknown expression TYPE" exp)))

(define (contents exp)
  (if (pair? exp)
      (cdr exp)
      (error "Unknown expression CONTENTS" exp)))

; used by query-driver-loop
(define (assertion-to-be-added? exp)
  (eq? (type exp) 'assert!))

(define (add-assertion-body exp)
  (car (contents exp)))

; special forms
(define (empty-conjunction? exps) (null? exps))
(define (first-conjunct exps) (car exps))
(define (rest-conjuncts exps) (cdr exps))
(define (empty-disjunction? exps) (null? exps))
(define (first-disjunct exps) (car exps))
(define (rest-disjuncts exps) (cdr exps))
(define (negated-query exps) (car exps))
(define (predicate exps) (car exps))
(define (args exps) (cdr exps))

; syntax of the rules 
(define (rule? statement)
  (tagged-list? statement 'rule))

(define (conclusion rule) (cadr rule))

(define (rule-body rule)
  (if (null? (cddr rule))
      '(always-true)
      (caddr rule)))

; transform (job ?x ?y) into (job (? x) (? y))
(define (query-syntax-process exp)
  (map-over-symbols expand-question-mark exp))

(define (map-over-symbols proc exp)
  (cond ((pair? exp)
         (cons (map-over-symbols proc (car exp))
               (map-over-symbols proc (cdr exp))))
        ((symbol? exp) (proc exp))
        (else exp)))

(define (expand-question-mark symbol)
  (let ((chars (symbol->string symbol)))
    (if (string=? (substring chars 0 1) "?")
        (list '?
              (string->symbol
               (substring chars 1 (string-length chars))))
        symbol)))

(define (var? exp) (tagged-list? exp '?))
(define (constant-symbol? exp) (symbol? exp))

; the unique id for a rule application is a number, which is incremented each time a rule is applied
(define rule-counter 0)

(define (new-rule-application-id)
  (set! rule-counter (+ 1 rule-counter))
  rule-counter)

(define (make-new-variable var rule-application-id)
  (cons '? (cons rule-application-id (cdr var))))

; when printing in driver loop, convert unbound vars back to the right form
(define (contract-question-mark variable)
  (string->symbol
   (string-append "?"
                  (if (number? (cadr variable))
                      (string-append (symbol->string (caddr variable))
                                     "-"
                                     (number->string (cadr variable)))
                      (symbol->string (cadr variable))))))
#+END_SRC 

** Frames and Bindings 

Frames are represented as a list of bindings, which are variable-value pairs

#+BEGIN_SRC scheme
(define (make-binding variable value)
  (cons variable value))

(define (binding-variable binding)
  (car binding))

(define (binding-value binding)
  (cdr binding))

(define (binding-in-frame variable frame)
  (assoc variable frame))

(define (extend variable value frame)
  (cons (make-binding variable value) frame))
#+END_SRC

