* Compilation 
:PROPERTIES:
:header-args: :session scheme :results verbatim raw
:ARCHIVE:
:END:

The explicit-control evaluator is a register machine whose controller interprets scheme programs. In this section, we will see how to run scheme programs on a register machine whose controller is not a scheme interpreter. 

Commercial general-purpose computers are register machines organized around a collection of registers and operations that constitute an efficient and convenient universal set of data paths. The controller for a general purpose machine is an interpreter for a register machine language like the one we have been using. This language is called the native language of the machine, or simply machine language. 

There are two common strategies for bridging the gap between higher-level languages and register-machine languages. 

The explicit-control evaluator illustrates the strategy of interpretation. An interpreter written in the native language of a machine configures the machine to execute programs writter in a language (the source language) that may differ from the native language of the machine performing the evaluation. The primitive procedures of the source language are implemented as a library of subroutines written in the native language of the given machine. A program to be interpreted (called the source program) is represented as a data structure. The interpreter traverses this data structure, analyzing the source program. As it does so, it simulates the intended behavior of the source program by calling appropriate primitive subroutines from the library. 

In this section, we explore the alternative strategy of compilation. A compiler for a given source language and machine translates a source program into an equivalent program (called the object program) written in the machine's native language. 

** An Overview of the Compiler  

Our compiler is much like our interpreter, both in its structure and in the function it performs. It will be easy to interface compiled code and interpreted code, as we will design the compiler to generate code that obeys the same conventions of register usage as the interpreter. 

This suggests a strategy for implementing a rudimentary compiler: We traverse the expression in the same way that the interpreter does, and when we encounter a register instruction that the interpreter would perform in evaluating the expression, we do not execute the instruction but instead accumulate it. The resulting sequence will be the object code. 

** Structure of the Compiler 

The procedure compile is the top-level dispatch in the compiler. Compile performs a case analysis on the syntactic type of the expression to be compiled. For each type of expression, it dispatches to a specialized code generator: 

#+BEGIN_SRC scheme
(define (compile exp target linkage)
  (cond ((self-evaluating? exp)
         (compile-self-evaluating exp target linkage))
        ((quoted? exp)
         (compile-quoted exp target linkage))
        ((variable? exp)
         (compile-variable exp target linkage))
        ((assignment? exp)
         (compile-assignment exp target linkage))
        ((definition? exp)
         (compile-definition exp target linkage))
        ((if? exp)
         (compile-if exp target linkage))
        ((lambda? exp)
         (compile-lambda exp target linkage))
        ((begin? exp)
         (compile-sequence
          (begin-actions exp) target linkage))
        ((cond? exp)
         (compile (cond->if exp) target linkage))
        ((application? exp)
         (compile-application exp target linkage))
        (else (error "Unknown expression type: COMPILE" exp))))
#+END_SRC

** Targets and Linkages

Compile and the code generators that it call also take two more args: 
- target specifies the register in which the compiled code is to return the value of the expression
- linkage descriptor - describes how the code resulting from the compilation of the expression should proceed when it has finished its execution 

The linkage descriptor can require that the code do one of three things:
- continue at the next instruction in the sequence; next
- return from the procedure being compiled; return
- jump to a named entry point; designated label 

** Instruction Sequences and Stack Usage 

Each code generator returns an instruction sequence containing the object code it has generated for the expression. Code generation for a compound expression is accomplished by combining the output from simpler code generators for component expressions.

An instruction sequence will contain three pieces of information: 
- The set of registers that must be initialized before the instructions in the sequence are executed
- The set of registers whose values are mofidied by the instructions in the sequence
- The actual instructions in the sequence 

We will represent an instruction sequence as a list of its three parts: 

#+BEGIN_SRC scheme
(define (make-instruction-sequence needs modifies statements)
  (list needs modifies statements))

; construct a sequence with no statements
(define (empty-instruction-sequence)
  (make-instruction-sequence '() '() '()))
#+END_SRC

** Compiling Expressions 

In this section and the next we implement the code generators to which the compile procedure dispatches. 

** Compiling Linkage Code 

In general, the output of each code generator will end with instructions that implement the required linkage. If the linkage is return, then we must generate the instruction (goto (reg continue)). If it is next, then we don't need additional instructions. Otherwise the linkage is a label, and we generate a goto to that label.

#+BEGIN_SRC scheme
(define (compile-linkage linkage)
  (cond ((eq? linkage 'return)
         (make-instruction-sequence '(continue) '() '((goto (reg continue)))))
        ((eq? linkage 'next)
         (make-instruction-sequence))
        (else (make-instruction-sequence '() '() `((goto (label ,linkage)))))))

(define (end-with-linkage linkage instruction-sequence)
  (preserving '(continue) instruction-sequence (compile-linkage linkage)))
#+END_SRC

** Coding Simple Expressions 

The code generators for self-evaluating expressions, quotations, and variables construct instruction sequences that assign the required value to the target register and then proceed as specified by the linkage descriptor. 

#+BEGIN_SRC scheme
; all of these assignment instructions modify the target register, and the one that looks up a variable modifies the environment register
(define (compile-self-evaluating exp target linkage)
  (end-with-linkage linkage (make-instruction-sequence '() (list target) `((assign ,target (const ,exp))))))

(define (compile-quoted exp target linkage)
  (end-with-linkage linkage (make-instruction-sequence '() (list target) `((assign ,target (const ,(text-of-quotation exp)))))))

(define (compile-variable exp target linkage)
  (end-with-linkage linkage (make-instruction-sequence '(env) (list target `((assign ,target (op lookup-variable-value) (const ,exp) (reg env)))))))
#+END_SRC

Assignment and definition are similar to the evaluator: we recursively generate code that computes the value to be assigned to the variable, and append to it a two-instruction sequence that sets or defines the variable and assigns the value of the whole expression to the target register. 

#+BEGIN_SRC scheme
(define (compile-assignment exp target linkage)
  (let ((var (assignment-variable exp))
        (get-value-code (compile (assignment-value exp) 'val 'next)))
    (end-with-linkage linkage
                      (preserving '(env) get-value-code
                                  (make-instruction-sequence '(env val) (list target)
                                                             `((perform (op set-variable-value!)
                                                                        (const ,var)
                                                                        (reg val)
                                                                        (reg env))
                                                               (assign ,target (const ok))))))))

(define (compile-definition exp target linkage)
  (let ((var (definition-variable exp))
        (get-value-code (compile (definition-value exp) 'val 'next)))
    (end-with-linkage linkage
                      (preserving '(env)
                                  get-value-code
                                  (make-instruction-sequence '(env val) (list target)
                                                             `((perform (op define-variable!)
                                                                        (const ,var)
                                                                        (reg val)
                                                                        (reg env))
                                                               (assign ,target (const ok))))))))
#+END_SRC

** Compiling Conditional Expressions 

The code for an if expression compiled with the given target and linkage has the form: 

#+BEGIN_SRC scheme
<compilation of predicate, target val, linkage next>
(test (op false?) (reg val))
(branch (label false-branch))
true-branch
<compilation of consequent with given target and given linkage or after-if>
false-branch
<compilation of alternative with given target and linkage>
after-if
#+END_SRC

#+BEGIN_SRC scheme
(define (compile-if exp target linkage)
  (let ((t-branch (make-label 'true-branch))
        (f-branch (make-label 'false-branch))
        (after-if (make-label 'after-if)))
    (let ((consequent-linkage
           (if (eq? linkage 'next) after-if linkage)))
      (let ((p-code (compile (if-predicate exp) 'val 'next))
            (c-code (compile (if-consequent exp) target consequent-linkage))
            (a-code (compile (if-alternative exp) target linkage)))
        (preserving '(env continue)
                    p-code
                    (append-instruction-sequences
                     (make-instruction-sequence '(val) '()
                                                `((test (op false?) (reg val))
                                                  (branch (label ,f-branch))))
                     (parallel-instruction-sequence
                      (append-instruction-sequences t-branch c-code)
                      (append-instruction-sequences f-branch a-code))
                     after-if))))))
#+END_SRC

** Compiling Sequences 

#+BEGIN_SRC scheme
(define (compile-sequence seq target linkage)
  (if (last-exp? seq)
      (compile (first-exp seq) target linkage)
      (preserving
       '(env continue)
       (compile (first-exp seq) target 'next)
       (compile-sequence (rest-exps seq) target linkage))))
#+END_SRC

** Compiling Lambda Expressions 

The object code for a lambda expression must have the form 

#+BEGIN_SRC scheme
<construct procedure object and assign it to target register>
<linkage>
#+END_SRC

#+BEGIN_SRC scheme
(define (compile-lambda exp target linkage)
  (let ((proc-entry (make-label 'entry))
        (after-lambda (make-label 'after-lambda)))
    (let ((lambda-linkage
           (if (eq? linkage 'next) after-lambda linkage)))
      (append-instruction-sequences
       (tack-on-instruction-sequence
        (end-with-linkage lambda-linkage
                          (make-instruction-sequence '(env) (list target)
                                                     `((assign ,target
                                                               (op make-compiled-procedure)
                                                               (label ,proc-entry)
                                                               (reg env)))))
        (compile-lambda-body exp proc-entry))
       after-lambda))))

(define (compile-lambda-body exp proc-entry)
  (let ((formals (lambda-parameters exp)))
    (append-instruction-sequences
     (make-instruction-sequence '(env proc argl) '(env)
                                `(,proc-entry
                                  (assign env (op compiled-procedure-env) (reg proc))
                                  (assign env (op extend-environment) (const ,formals) (reg argl) (reg env))))
     (compile-sequence (lambda-body exp) 'val 'return))))
#+END_SRC

** Compiling Combinations 

The code for a combination compiled with a given target and linkage has the form: 

#+BEGIN_SRC scheme
<compilation of operator, target proc, linkage next>
<evaluate operands and construct argument list in argl>
<compilation of procedure call with given target and linkage>
#+END_SRC

#+BEGIN_SRC scheme
(define (compile-application exp target linkage)
  (let ((proc-code (compile (operator exp) 'proc 'next))
        (operand-codes (map (lambda (operand)
                              (compile operand 'val 'next))
                            (operands exp))))
    (preserving '(env continue)
                proc-code
                (preserving '(proc continue)
                            (construct-arglist operand-codes)
                            (compile-procedure-call target linkage)))))

(define (construct-arglist operand-codes)
  (let ((operand-codes (reverse operand-codes)))
    (if (null? operand-codes)
        (make-instruction-sequeence '() '(argl)
                                    '((assign argl (const ()))))
        (let ((code-to-get-last-arg
               (append-instruction-sequences
                (car operand-codes)
                (make-instruction-sequence '(val) '(argl)
                                           '((assign argl (op list) (reg val)))))))
          (if (null? (cdr operand-codes))
              code-to-get-last-arg
              (preserving '(env)
                          code-to-get-last-arg
                          (code-to-get-rest-args
                           (cdr operand-codes))))))))

(define (code-to-get-rest-args operand-codes)
  (let ((code-for-next-arg
         (preserving '(argl)
                     (car operand-codes)
                     (make-instruction-sequence '(val argl) '(argl) '((assign argl (op cons) (reg val) (reg argl)))))))
    (if (null? (cdr operand-codes))
        code-for-next-arg
        (preserving '(env)
                    code-for-next-arg
                    (code-to-get-rest-args (cdr operand-codes))))))
#+END_SRC

** Applying Procedures 

After evaluating the elements of a combination, the compiled code must apply the procedure in proc to the arguments in argl. This is essentially the same as the apply procedure in the metacircular evaluator, or apply-dispatch in the explicit-control evaluator. 

#+BEGIN_SRC scheme
; form
(test (op primitive-procedure?) (reg proc))
(branch (label primitive-branch))
compiled-branch
<code to apply compiled proc with given target and appropriate linkage>
primitive-branch
(assign target (op apply-primitive-procedure) (reg proc) (reg argl))
<linkage>
after-call

(define (compile-procedure-call target linkage)
  (let ((primitive-branch (make-label 'primitive-branch))
        (compiled-branch (make-label 'compiled-branch))
        (after-call (make-label 'after-call)))
    (let ((compiled-linkage
           (if (eq? linkage 'next) after-call linkage)))
      (append-instruction-sequences
       (make-instruction-sequence '(proc) '()
                                  `((test (op primitive-procedure?) (reg proc))
                                    (branch (label ,primitive-branch))))
       (parallel-instruction-sequences
        (append-instruction-sequences
         compiled-branch
         (compile-proc-appl target compiled-linkage))
        (append-instruction-sequences
         primitive-branch
         (end-with-linkage linkage
                           (make-instruction-sequence '(proc argl)
                                                      (list target)
                                                      `((assign ,target
                                                                (op apply-primitive-procedure)
                                                                (reg proc) (reg argl)))))))
       after-call))))
#+END_SRC

** Applying Compiled Procedures 

The code that handles procedure application is the most subtle part of the compiler, even though the instruction sequences it generates are very short. A compiled procedure has an entry point, which ius a label that designates where the code for the procedure starts. The code at this entry point computes a result in val and returns by executing the instruction (goto (reg continue)). 

#+BEGIN_SRC scheme
; we expect the code for a compiled procedure application to look like this if the linkage is a label
(assign continue (label proc-return))
(assign val (op compiled-procedure-entry (reg proc)))
(goto (reg val))
proc-return
(assign target (reg val)) ; included if target is not val
(goto (label linkage)) ; linkage code

; or this if linkage is return
(save continue)
(assign continue (label proc-return))
(assign val (op compiled-procedure-entry) (reg proc))
(goto (reg val))
proc-return
(assign target (reg val))
(restore continue)
(goto (reg continue))

(define (compile-proc-appl target linkage)
  (cond ((and (eq? target 'val)
              (not (eq? linkage 'return)))
         (make-instruction-sequence '(proc) all-regs
                                    `((assign continue (label ,linkage))
                                      (assign val (op compiled-procedure-entry) (reg proc))
                                      (goto (reg val)))))
        ((and (not (eq? target 'val))
              (not (eq? linkage 'return)))
         (let ((proc-return (make-label 'proc-return)))
           (make-instruction-sequence '(proc) all-regs
                                      `((assign continue (label ,proc-return))
                                        (assign val (op compiled-procedure-entry) (reg proc))
                                        (goto (reg val))
                                        ,proc-return
                                        (assign ,target (reg val))
                                        (goto (label ,linkage))))))
        ((and (eq? target 'val)
              (eq? linkage 'return))
         (make-instruction-sequence
          '(proc continue) all-regs
          '((assign val (op compiled-procedure-entry) (reg proc))
            (goto (reg val)))))
        ((and (not (eq? target 'val))
              (eq? linkage 'return))
         (error "return linkage, target not val: COMPILE" target))))
#+END_SRC

** Combining Instruction Sequences

This section describes the details on how instruction sequences are represented and combined. Recall that an instruction sequence is represented as a list of the registers needed, the registers modified, and the actual instructions. 

#+BEGIN_SRC scheme
; determine the registers needed
(define (registers-needed s)
  (if (symbol? s) '() (car s)))

(define (registers-modified s)
  (if (symbol? s) '() (cadr s)))

(define (statements s)
  (if (symbol? s)
      (list s)
      (caddr s)))

; determine whether a given sequence needs or modifies a given register
(define (needs-register? seq reg)
  (memq reg (registers-needed seq)))

(define (modifies-register? seq reg)
  (memq reg (registers-modified seq)))
#+END_SRC

In terms of these predicates and selectors we can implement the various instruction sequence combininers used throughout the compiler. 

#+BEGIN_SRC scheme
(define (append-instruction-sequences . seqs)
  (define (append-2-sequences seq1 seq2)
    (make-instruction-sequence
     (list-union
      (registers-needed seq1)
      (list-difference (registers-needed seq2)
                       (registers-modified seq1)))
     (list-union (registers-modified seq1)
                 (registers-modified seq2))
     (append (statements seq1) (statements seq2))))
  (define (append-seq-list seqs)
    (if (null? seqs)
        (empty-instruction-sequence)
        (append-2-sequences
         (car seqs)
         (append-seq-list (cdr seqs)))))
  (append-seq-list seqs))

(define (list-union s1 s2)
  (cond ((null? s1) s2)
        ((memq (car s1) s2)
         (list-union (cdr s1) s2))
        (else (cons (car s1) (list-union (cdr s1) s2)))))

(define (list-difference s1 s2)
  (cond ((null? s1) '())
        ((memq (car s1) s2)
         (list-difference (cdr s1) s2))
        (else (cons (car s1)
                    (list-difference (cdr s1) s2)))))

(list-difference (list 1 2 3 4) (list 2 3 4 5))

(define (preserving regs seq1 seq2)
  (if (null? regs)
      (append-instruction-sequences seq1 seq2)
      (let ((first-reg (car regs)))
        (if (and (needs-register? seq2 first-reg)
                 (modifies-register? seq1 first-reg))
            (preserving (cdr regs)
                        (make-instruction-sequence
                         (list-union (list first-reg)
                                     (registers-needed seq1))
                         (list-difference (registers-modified seq1)
                                          (list first-reg))
                         (append `((save ,first-reg))
                                 (statements seq1)
                                 `((restore ,first-reg))))
                        seq2)
            (preserving (cdr regs) seq1 seq2)))))

; used by compile-lambda to append a procedure body to another sequence
(define (tack-on-instruction-sequence seq body-seq)
  (make-instruction-sequence (registers-needed seq)
                             (registers-modified seq)
                             (append (statements seq)
                                     (statements body-seq))))

; used in compile-if and compile-procedure-call to append two alternative branches that follow a test
(define (parallel-instruction-sequences seq1 seq2)
  (make-instruction-sequence
   (list-union (registers-needed seq1)
               (registers-needed seq2))
   (list-union (registers-modified seq1)
               (registers-modified seq2))
   (append (statements seq1)
           (statements seq2))))
#+END_SRC

We have now seen all the elements of a compiler! 

** Lexical Addressing

One of the most common optimizations performed by compilers is the optimization of variable lookup. Our compiler generates code that uses the lookup-variable-value operation of the evaluator machine. This searches for a variable by comparing it with each variable that is currently bound, working frame by frame outward through the run-time environment. 

Since our language is lexically scoped, the runtime environment for any expression will have a structure that parallels the lexical structure of the program in which the expression appears. We can exploit this by inventing a new kind of variable lookup operation, lexical-address-lookup, that takes as args an environment and a lexical address that consists of two numbers: a frame number, which specifies how many frames to pass over, and a displacement number which specifies how many variables to pass over in that frame. 

One way for the compiler to produce code that uses lexical addressing is to maintain a data structure called a compile time environment. This keeps track of which variables will be at which positions in which frames in the run-time environment when a particular variable-access operation is executed. The compile-time environment becomes an additional argument to compile that is passed along to each code generator. 

** Interfacing Compiled Code to the Evaluator 

We have not looked at how to load compiled code into the evaluator machine or how to run it. 

#+BEGIN_SRC scheme
(define (compile-and-go expression)
  (let ((instructions
         (assemble
          (statements
           (compile expression 'val 'return))
          eceval)))
    (set! the-global-environment (setup-environment))
    (set-register-contents! eceval 'val instructions)
    (set-register-contents! eceval 'flag true)
    (start exeval)))
#+END_SRC

** Interpretation and Compilation 

An interpreter raises the machine to the level of the user program; a compiler lowers the user program to the level of the machine language. We can regard a programming language as a coherent family of abstractions erected on the machine language. 
